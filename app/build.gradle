apply plugin: 'com.android.application'

def acraVersion = '5.7.0'
def keystorePropertiesFile = rootProject.file("../keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
def sFile = file(keystoreProperties['storeFile'])
def sPassword = keystoreProperties['storePassword']
def kAlias = keystoreProperties['keyAlias']
def kPassword = keystoreProperties['keyPassword']

android {
    signingConfigs {
        v1 {
            storeFile sFile
            storePassword sPassword
            keyAlias kAlias
            keyPassword kPassword
            v1SigningEnabled true
            v2SigningEnabled false
        }
        v2 {
            storeFile sFile
            storePassword sPassword
            keyAlias kAlias
            keyPassword kPassword
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    compileSdk 30
    defaultConfig {
        applicationId "org.strawing.customiuizermod"
        minSdkVersion 24
        //noinspection OldTargetApi,ExpiredTargetSdkVersion
        targetSdkVersion 27
        versionCode 71
        versionName "1.0.3.2.1"
        resConfigs 'ru-rRU', 'uk-rUK', 'zh-rCN', 'pt-rBR', 'de', 'es', 'it', 'tr'
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions 'api'
    productFlavors {
        standalone {
            targetSdkVersion 29
            signingConfig signingConfigs.v1
        }
        playstore {
            targetSdkVersion 30
            signingConfig signingConfigs.v2
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/*.kotlin_module', '**.kotlin_builtins', '**.kotlin_metadata']
        }
    }
    androidResources {
        noCompress 'zip'
    }
//    compileOptions {
//        sourceCompatibility JavaVersion.VERSION_1_8
//        targetCompatibility JavaVersion.VERSION_1_8
//    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "ch.acra:acra-core:$acraVersion"
    //noinspection DifferentStdlibGradleVersion
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.10'
    implementation 'com.github.jinatonic.confetti:confetti:1.1.2'
    implementation files('libs/WeatherView-2.0.3.aar')
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly files('lib/miui.jar')
    compileOnly files('lib/miuisystem.jar')
    compileOnly files('lib/framework.jar')
}